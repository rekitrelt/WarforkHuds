setCursor #WIDTH / 2, 0

// Top score/clock background
setAlign 2 1
setSize 395 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 40
setColor 0 0 0 0.5
setColorAlpha 0.25
if %TEAM > 1 // if not #TEAM_PLAYER or #TEAM_SPECTATOR
	setColorToTeamColor %TEAM
	setColorAlpha 0.25
endif
if %TEAM == #TEAM_SPECTATOR
	setColorAlpha 0.75
endif
drawPicByName "gfx/hud/default/top_bar"

//clock numbers
moveCursor 0 -1
setFontFamily "Goldman"
setFontSize %VIDHEIGHT / 16

//pro outline
setColor 0 0 0 1
drawClock
moveCursor 0, -2
drawClock
moveCursor -2, 0
drawClock
moveCursor 0, 2
drawClock
moveCursor 1, -1

setColor 1 1 1 1
if %SHOW_PICKUP > 0
	if %PICKUP_ITEM == #ITEM_RED_ARMOR
		setColor 0.9 0.2 0.2 1 // red
	endif
	if %PICKUP_ITEM == #ITEM_YELLOW_ARMOR
		setColor 1 0.9 0.2 1 // yellow
	endif
	if %PICKUP_ITEM == #ITEM_GREEN_ARMOR
		setColor 0.3 0.9 0.3 1 // green
	endif
	if %PICKUP_ITEM == #ITEM_MEGA_HEALTH
		setColor 1 0.3 1 1 // magenta
	endif
	if %PICKUP_ITEM == #ITEM_ULTRA_HEALTH
		setColor 0.2 0.4 1.0 1 // blue
	endif
	if %PICKUP_ITEM == #ITEM_QUAD_DAMAGE
		setColor 1 0.5 0 1 // orange
	endif
	if %PICKUP_ITEM == #ITEM_WARSHELL
		setColor 0.2 1 1 1 // lightblue
	endif
	if %PICKUP_ITEM == #ITEM_REGENERATION
		setColor 0.9 0.2 0.2 1 // red
	endif
endif
drawClock
moveCursor +0.5, +0.5

// MATCH STATES
setFontFamily "Orbitron"
setFontStyle Bold
setFontSize %VIDHEIGHT / 48
setAlign 2 1
setCursor #WIDTH / 2, 40
moveCursor 0 2
setColor 0.4 1 0.4 0.5
if %MATCH_STATE == #MATCH_STATE_WARMUP
	drawString "Warmup"
endif
if %OVERTIME
	drawString "Overtime"
endif
setFontStyle Normal
